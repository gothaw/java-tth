plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.17.7'
}

group 'com.teamtreehouse.pomodoro'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

javafx {
    version = '14'
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.media']
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

//     For Fat Jar Creation - Not Compatible with :jlink task - creates error in JavaFX version
gradle.taskGraph.whenReady {
    graph ->
        if (!graph.hasTask(":jlink")) {
            jar {
                manifest {
                    attributes 'Main-Class': 'com.teamtreehouse.pomodoro.EntryPoint'
                }
                from {
                    configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
                }
            }
        }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    jpackage {
        jpackageHome = 'C:/Program Files/Java/jdk-14.0.1'
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu']
        }
    }
    customImage {
        appModules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
    }
    launcher {
        name = 'pomodoro-app'
    }
}

application {
    mainClassName = 'com.teamtreehouse.pomodoro.EntryPoint'
}
